 req←Handler req;gui;evt;data;en;msg;opts;os;as;script;nl;number;expression;message;filename;dir;id;FileName
 (gui evt id data en msg)←req 
 opts←0 ⎕JSON data
⍝ Turn shifting key options into a list of shifts
 os←opts.⎕NL ¯2 ⋄ as←##.ahk_shifts.⎕NL ¯2
 opts.shifts←os∩as
⍝ Build and save AHK script, report success/failure to front end
 :Trap 0
     :If 0=≢opts.shifts
         id∘Send 1 ⎕JSON(1 'Please select at least 1 shifting key')
     :Else
         script←##.GenerateScript opts
         FileName←{'APL-',⍵.locale,'-',(⊃,/⍵.shifts),'.ahk'}
         filename←FileName opts
         dir←opts.outpath,'\' ⍝⊃1 ⎕NPARTS''
         script ⎕NPUT(dir,filename)1
         message←'Saved: ',dir,filename
         id∘Send 1 ⎕JSON(0 message)
     :EndIf
 :Else
     nl←⎕UCS 10
     (number expression)←⎕DMX.(EN DM)
     expression←1↓∊nl∘,¨expression
     id∘Send 1 ⎕JSON(number expression)
 :EndTrap
